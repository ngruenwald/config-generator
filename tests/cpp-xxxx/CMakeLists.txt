cmake_minimum_required(VERSION 3.20)

project(test LANGUAGES CXX)

set(CMAKE_CTEST_ARGUMENTS "--output-on-failure")
enable_testing()

if(NOT DEFINED TEMPLATE)
  set(TEMPLATE "all")
endif()

message(STATUS "TEMPLATE: ${TEMPLATE}")

include(FetchContent)
include(cgen.cmake)

if("${TEMPLATE}" STREQUAL "all")
  set(USE_JSON ON)
  set(USE_PUGIXML ON)
  set(USE_XMLWRP ON)
  set(USE_YAML ON)
elseif("${TEMPLATE}" STREQUAL "cpp-json")
  set(USE_JSON ON)
elseif("${TEMPLATE}" STREQUAL "cpp-pugixml")
  set(USE_PUGIXML ON)
elseif("${TEMPLATE}" STREQUAL "cpp-xmlwrp")
  set(USE_XMLWRP ON)
elseif("${TEMPLATE}" STREQUAL "cpp-yaml")
  set(USE_YAML ON)
else()
  message(FATAL_ERROR "unsupported template: ${TEMPLATE}")
endif()


if(USE_JSON)
  set(JSON_BuildTests OFF)
  set(JSON_MultipleHeaders OFF)
  FetchContent_Declare(json GIT_REPOSITORY https://github.com/nlohmann/json.git GIT_TAG v3.11.2)
  FetchContent_MakeAvailable(json)
  magic(TEMPLATE cpp-json DEPENDENCY nlohmann_json::nlohmann_json TESTFILE example.json)
endif()
  
if(USE_PUGIXML)
  FetchContent_Declare(pugixml GIT_REPOSITORY https://github.com/zeux/pugixml.git GIT_TAG v1.14)
  FetchContent_MakeAvailable(pugixml)
  magic(TEMPLATE cpp-pugixml DEPENDENCY pugixml::static TESTFILE example.xml)
endif()

if(USE_XMLWRP)
  find_package(xmlwrp 3.4 QUIET)
  if(xmlwrp_FOUND)
    magic(TEMPLATE cpp-xmlwrp DEPENDENCY xmlwrp::xmlwrp TESTFILE example.xml)
  else()
    message(WARNING "xmlwrp not found - skipping test")
  endif()
endif()

if(USE_YAML)
  set(YAML_BUILD_SHARED_LIBS OFF)
  set(YAML_CPP_BUILD_TESTS OFF)
  FetchContent_Declare(yaml_cpp GIT_REPOSITORY https://github.com/jbeder/yaml-cpp.git GIT_TAG 0.8.0)
  FetchContent_MakeAvailable(yaml_cpp)
  magic(TEMPLATE cpp-yaml DEPENDENCY yaml-cpp::yaml-cpp TESTFILE example.yml)
endif()
